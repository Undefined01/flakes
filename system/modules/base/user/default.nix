{
  pkgs,
  lib,
  config,
  inputs,
  outputs,
  isLinux,
  isWsl,
  isDarwin,
  ...
}:

let
  inherit (lib) types mkOption;

  homeManagerModule =
    if isDarwin then
      inputs.home-manager.darwinModules.home-manager
    else
      inputs.home-manager.nixosModules.home-manager;

  userOptions =
    { config, name, ... }:
    {
      options = {
        name = mkOption {
          type = types.str;
          default = name;
          example = "lh";
          description = ''
            The user name for the user this host specification
            applies to.
          '';
        };

        authorizedKeys = mkOption {
          type = types.listOf types.str;
          default = [ ];
          description = ''
            A list of SSH public keys that will be added to the
            user's authorized_keys file.
          '';
        };

        homeConfiguration = mkOption {
          type = types.nullOr types.path;
          default = null;
          example = "lib.custom.fromFlakeRoot \"home/top/nixos/wsl\"";
          description = ''
            The path to the Home Manager configuration for this user.
          '';
        };
      };
    };
in
{
  imports = [
    homeManagerModule
  ];

  options = {
    hostSpec.users = mkOption {
      type = types.attrsOf (types.submodule userOptions);
      description = "Host-specific user options.";
      default = { };
    };

    hostSpec.primaryUser = mkOption {
      type = types.nullOr types.str;
      default = null;
      description = "The primary user on this system. Used for setting the login user in NixOS-WSL.";
    };
  };

  config = lib.mkMerge [
    {
      home-manager = {
        useUserPackages = true;
        extraSpecialArgs = {
          inherit inputs outputs;
        };
      };
    }

    (lib.optionalAttrs isLinux {
      users = {
        mutableUsers = false;
        # Generated by `mkpasswd`
        users.root.initialHashedPassword = lib.mkDefault "$y$j9T$k3PWrdywWLH7PwDusAq2i.$eSsxc34TvdW1j0J7DjQnqtaTMvgFxVFYlVsBYdffnlA";
      };
    })

    {
      users = lib.mkMerge (
        map (cfg: {
          groups.${cfg.name} = { };
          users.${cfg.name} = {
            home = lib.mkDefault (if pkgs.stdenv.isDarwin then "/Users/${cfg.name}" else "/home/${cfg.name}");
            openssh.authorizedKeys.keys = cfg.authorizedKeys;
          }
          // lib.optionalAttrs isLinux {
            isNormalUser = true;
            group = cfg.name;
            extraGroups = [
              "wheel"
              "users"
            ];
            initialPassword = cfg.name;
          };
        }) (builtins.attrValues config.hostSpec.users)
      );
      home-manager.users = lib.mkMerge (
        map (
          cfg:
          lib.mkIf (cfg.homeConfiguration != null) {
            ${cfg.name} = {
              imports = [ cfg.homeConfiguration ];
              _module.args = {
                user = cfg.name;
                inherit
                  lib
                  isLinux
                  isWsl
                  isDarwin
                  ;
              };
            };
          }
        ) (builtins.attrValues config.hostSpec.users)
      );
    }
  ];
}
